expr = {
    call
  | list
  | functiondef
  | macrodef
  | data_list
  | macro_remainder
  | identifier
  | float
  | integer
  | string
  | bool
  | nil
}

identifier = @{
    (!reserved_word ~ 'a'..'z' ~ ('a'..'z' | '0'..'9' | "-")* ~ "?"?)
  | ("+" | "-" | "*" | "/" | "\\" | "|" | "!" | "?" | "$" | "%" | "^" | "~" | ">" | "<" | "=" | "&" | "@")+
}

reserved_word      = { bool | nil | "func" | "macro"}

list       = { "(" ~ expr* ~ ")" }
data_list  = { "[" ~ expr* ~ "]" }
ident_list = { "(" ~ identifier* ~ ")" }
macro_remainder = @{identifier ~ "..."}


call   = { "(" ~ identifier ~ expr* ~ ")" }
functiondef = { "(" ~ "func" ~ ident_list ~ expr ~ ")" }
macrodef  = { "(" ~ "macro" ~ ident_list ~ expr ~ ")" }

float   = @{ ASCII_DIGIT* ~ "." ~ ASCII_DIGIT+ | ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT*? }
integer = @{ ASCII_DIGIT+ }

string = ${ "\"" ~ string_inner ~ "\"" }
string_inner = @{(!"\"" ~ ANY)*}

bool  = { true | false }
true  = { "true" }
false = { "false" }
nil   = { "nil" }

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT = _{"#[" ~ (!"]#" ~ ANY)* ~ "]#" | "#" ~ (!NEWLINE ~ ANY)* ~ NEWLINE}