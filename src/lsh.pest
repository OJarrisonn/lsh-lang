expr = {
    call
  | list
  | lambda
  | macro
  | identifier
  | macro_identifier
  | data_list
  | float
  | integer
  | string
  | bool
  | nil
}

identifier = @{
    (!reserved_word ~ 'a'..'z' ~ ('a'..'z' | '0'..'9' | "-")* ~ "?"?)
  | !reserved_operators ~ ("+" | "-" | "*" | "/" | "\\" | "|" | "!" | "?" | "$" | "%" | "^" | "~" | ">" | "<" | "=" | "&" | "@")+
}

macro_identifier = @{ "@" ~ (!reserved_word ~ ('a'..'z') ~ ('a'..'z' | '0'..'9' | "-")*) }

reserved_word      = { bool | nil }
reserved_operators = { "&" | "@" }

list       = { "(" ~ expr* ~ ")" }
data_list  = { "[" ~ expr* ~ "]" }
ident_list = { "(" ~ identifier* ~ ")" }

call   = { "(" ~ (identifier | macro_identifier) ~ expr* ~ ")" }
lambda = { "(" ~ "&" ~ ident_list ~ list ~ ")" }
macro  = { "(" ~ "@" ~ ident_list ~ list ~ ")" }

float   = @{ ASCII_DIGIT* ~ "." ~ ASCII_DIGIT+ | ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT*? }
integer = @{ ASCII_DIGIT+ }

string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

bool  = { true | false }
true  = { "true" }
false = { "false" }
nil   = { "nil" }

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT = _{"#[" ~ (!"]#" ~ ANY)* ~ "]#" | "#" ~ (!NEWLINE ~ ANY)* ~ NEWLINE}