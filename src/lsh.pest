expr = {
    call
  | list
  | function
  | macro
  | macro_identifier
  | identifier
  | data_list
  | macro_remainder
  | float
  | integer
  | string
  | bool
  | nil
}

identifier = @{
    (!reserved_word ~ 'a'..'z' ~ ('a'..'z' | '0'..'9' | "-")* ~ "?"?)
  | !reserved_operators ~ ("+" | "-" | "*" | "/" | "\\" | "|" | "!" | "?" | "$" | "%" | "^" | "~" | ">" | "<" | "=" | "&" | "@")+
}

macro_identifier = @{ "@" ~ (!reserved_word ~ ('a'..'z') ~ ('a'..'z' | '0'..'9' | "-")*) }

reserved_word      = { bool | nil | "func" | "macro"}
reserved_operators = { macro_remainder | "@" }

list       = { "(" ~ expr* ~ ")" }
data_list  = { "[" ~ expr* ~ "]" }
ident_list = { "(" ~ identifier* ~ ")" }
macro_remainder = {"..."}


call   = { "(" ~ (macro_identifier | identifier) ~ expr* ~ ")" }
function = { "(" ~ "func" ~ ident_list ~ list ~ ")" }
macro  = { "(" ~ "macro" ~ ident_list ~ list ~ ")" }

float   = @{ ASCII_DIGIT* ~ "." ~ ASCII_DIGIT+ | ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT*? }
integer = @{ ASCII_DIGIT+ }

string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

bool  = { true | false }
true  = { "true" }
false = { "false" }
nil   = { "nil" }

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT = _{"#[" ~ (!"]#" ~ ANY)* ~ "]#" | "#" ~ (!NEWLINE ~ ANY)* ~ NEWLINE}